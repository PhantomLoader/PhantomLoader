subprojects {
    apply plugin: 'java'
    apply plugin: 'signing'
    apply plugin: 'maven-publish'

    // Group id and mod version
    group = 'io.github.phantomloader'
    version = phantomVersion

    // Mojang ships Java 17 to end users in 1.18+
    java.toolchain.languageVersion = JavaLanguageVersion.of(17)

    // Jar manifest attributes
    jar {
        manifest {
            attributes([
                    "Specification-Title": rootProject.name,
                    "Specification-Vendor": 'HexagonNico',
                    "Specification-Version": project.version,
                    "Implementation-Title": project.name,
                    "Implementation-Version": project.jar.archiveVersion,
                    "Implementation-Vendor": 'HexagonNico',
                    "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
            ])
        }
    }

    // Sources and javadoc jars
    java {
        withJavadocJar()
        withSourcesJar()
    }

    // Maven central publication
    publishing {
        publications {
            maven(MavenPublication) {
                groupId = project.group
                artifactId = artifactId
                version = project.version
                from components.java

                pom {
                    name = rootProject.name
                    description = 'A library that can automate the process of creating minecraft mods for multiple loaders'
                    url = 'https://github.com/PhantomLoader'

                    scm {
                        connection = 'git@github.com:PhantomLoader/PhantomLoader.git'
                        url = 'https://github.com/PhantomLoader/PhantomLoader'
                    }

                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }

                    developers {
                        developer {
                            id = 'HexagonNico'
                            name = 'Nicholas Amigoni'
                            email = 'nico.hex6@gmail.com'
                        }
                    }

                    repositories {
                        maven {
                            name = "Sonatype"
                            url = 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
                            credentials {
                                username = System.getenv('OSSRH_USERNAME')
                                password = System.getenv('OSSRH_PASSWORD')
                            }
                        }
                    }
                }
            }
        }
    }

    // The metadata includes mapped dependencies which are not reasonably consumable by other mod developers.
    tasks.withType(GenerateModuleMetadata).configureEach {
        enabled = false
    }

    // Code signing
    signing {
        def signingKey = findProperty('signingKey')
        def signingPassword = findProperty('signingPassword')
        useInMemoryPgpKeys(signingKey, signingPassword)
        sign publishing.publications.maven
    }
}