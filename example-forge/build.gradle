plugins {
    id 'idea'
    id 'java-library'
    id 'net.minecraftforge.gradle' version '[6.0,6.2)'
}

base {
    archivesName = 'example-forge'
}

minecraft {
    mappings channel: 'official', version: minecraftVersion

    runs {
        client {
            workingDirectory this.file('../.minecraft')
            ideaModule "${rootProject.name}.${project.name}.main"
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            mods {
                runClient {
                    source this.sourceSets.main
                }
            }
        }
    }
}

sourceSets.main.resources.srcDir 'src/generated/resources'

dependencies {
    minecraft "net.minecraftforge:forge:${minecraftVersion}-${forgeVersion}"
    api project(":example-common")
    annotationProcessor project(':processor-forge')
}

tasks.withType(JavaCompile).configureEach {
    source project(":example-common").sourceSets.main.allSource
}

processResources {
    from project(":example-common").sourceSets.main.resources
}

build.finalizedBy('genIntellijRuns')

jar.finalizedBy('reobfJar')

compileJava {
    options.compilerArgs.add "-AminecraftVersion=${minecraftVersion}"
    options.compilerArgs.add "-AphantomVersion=${phantomVersion}"
    options.compilerArgs.add "-AforgeVersion=${forgeVersion}"
}
