plugins {
    id 'idea'
    id 'java-library'
    id 'net.minecraftforge.gradle' version '[6.0,6.2)'
}

base {
    archivesName = 'example'
}

minecraft {
    mappings channel: 'official', version: '1.20.1'

    runs {
        client {
            workingDirectory this.file('../run')
            ideaModule "${rootProject.name}.${project.name}.main"
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            property 'forge.enabledGameTestNamespaces', 'example'
            mods {
                test {
                    source this.sourceSets.main
                }
            }
        }
    }
}

sourceSets.main.resources.srcDir 'src/generated/resources'

dependencies {
    minecraft "net.minecraftforge:forge:1.20.1-47.2.0"
    api project(":example-common")
    annotationProcessor project(':processor-forge')
}

tasks.withType(JavaCompile).configureEach {
    source project(":example-common").sourceSets.main.allSource
}

processResources {
    from project(":example-common").sourceSets.main.resources
}

jar {
    archiveClassifier = 'forge'
}

build.finalizedBy('genIntellijRuns')

jar.finalizedBy('reobfJar')
