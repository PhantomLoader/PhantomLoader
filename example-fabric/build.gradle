plugins {
    id 'java'
    id 'fabric-loom' version '0.12-SNAPSHOT'
}

base {
    archivesName = 'example-fabric'
}

repositories {
    flatDir {
        dir '../library-fabric/build/libs'
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${minecraftVersion}"
    mappings loom.officialMojangMappings()
    modImplementation "net.fabricmc:fabric-loader:${fabricVersion}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${fabricApiVersion}+${minecraftVersion}"
    implementation project(':example-common')
    modImplementation "io.github.phantomloader:library-fabric:${phantomVersion}"
    annotationProcessor project(':processor-fabric')
}

loom {
    runs {
        client {
            client()
            setConfigName("fabric-1.20")
            ideConfigGenerated(true)
            runDir("../.minecraft")
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    source project(':example-common').sourceSets.main.allSource
    options.compilerArgs.add "-AminecraftVersion=${minecraftVersion}"
    options.compilerArgs.add "-AphantomVersion=${phantomVersion}"
    options.compilerArgs.add "-AfabricVersion=${fabricVersion}"
    options.compilerArgs.add "-AfabricApiVersion=${fabricApiVersion}"
    options.compilerArgs.add "-AmodId=example"
    options.compilerArgs.add "-AmodName=Example Mod"
    options.compilerArgs.add "-AmodLicense=CC BY-NC-ND 4.0"
    options.compilerArgs.add "-AmodGroupId=io.github.phantomloader.example"
    options.compilerArgs.add "-AmodAuthors=ExampleAuthor1, ExampleAuthor2"
    options.compilerArgs.add "-AmodDescription=This is an autogenerated mod"
}

processResources {
    from project(':example-common').sourceSets.main.resources
}
